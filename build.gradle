// Establish version and status ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name
apply plugin: 'java'
sourceCompatibility = 1.7

buildscript {
    repositories {
      mavenCentral() 
      maven {
         url 'https://github.com/steffenschaefer/gwt-gradle-plugin/raw/maven-repo/'
      }
      maven {
         url 'https://maven.sencha.com/repo/support-commercial-release'
      }
    }
    dependencies { 
      classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.3'
    }
}

// Establish extended properties for all projects
ext {
   gwtVer = "2.5.1"
   gxtVersion = "3.0.6"
   log4jVersion = "1.2.16"
   slf4jVersion = "1.7.6"
   jUnitVersion = "4.11"
   remoteUri = ""
   remoteRef = ""
   subtreePrefix = ""
}

dependencies {
   compile project(':de')
   compile project(':belphegor')
}

// Set dependencies for all sub projects
subprojects {
   apply plugin: 'java'
   sourceCompatibility = 1.7

   repositories {
      mavenLocal()
      mavenCentral()
   }

   group = 'org.iplantc.de'

   jar {
      from sourceSets.main.allJava
   }
         
   // Exclude all GwtTests from normal java compiles
   test {
        exclude '**/GwtTest**'
        // Disable the test report for the individual test task
        reports.html.enabled = false
   }
   sourceSets.main.resources.srcDir 'src/main/java'
}

task testReport(type: TestReport) {
    group "Verification"
    description "Aggregates all sub-project jUnit tests reports"
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

apply from: 'iplant/webapps.gradle'
apply from: 'iplant/modules.gradle'

apply from: 'iplant/eclipse.gradle'
apply from: 'iplant/webappBootstrap.gradle'

